Bubbly Iron Orangutan

High

# Rewards May Not Be Claimed When Depositing

## Summary
The `StakedEXA.sol#claim_()` function is designed to claim rewards for the caller. So, when a deposit occurs, the address (`to`) that receives the tokens cannot simultaneously claim its rewards. This oversight may result in a loss of rewards for users during the deposit process.

## Vulnerability Detail
The `StakedEXA.sol#_update()` function is responsible for managing updates during token transfers, such as deposits and withdrawals. Here's the relevant code snippet:
```solidity
function _update(address from, address to, uint256 amount) internal override whenNotPaused {
    if (from == address(0)) {
        // Minting new tokens
        SNIP...
        
        if (time > memRefTime) {
-->       if (balance != 0) claimWithdraw(reward, to, balance); // L143 claim withdraw
            avgIndexes[to][reward] = rewards[reward].index;
        } else {
-->       if (balance != 0) claim_(reward); // L146 claim for the caller
            uint256 numerator = avgIndexes[to][reward] * balance + rewards[reward].index * amount;
            avgIndexes[to][reward] = numerator == 0 ? 0 : (numerator - 1) / total + 1;
        }
    }
    // SNIP...
}
```
These lines handle claiming rewards at the time of deposit. However, the `claim_()` function is currently defined as follows:
```solidity
function claim_(IERC20 reward) internal whenNotPaused {
    uint256 time = block.timestamp * 1e18 - avgStart[msg.sender];
    if (time <= minTime * 1e18) return; // Check if claiming is possible

    uint256 claimedAmount = claimed[msg.sender][reward];
    uint256 claimableAmount = Math.max(rawClaimable(reward, msg.sender, balanceOf(msg.sender)), claimedAmount);
    uint256 claimAmount = claimableAmount - claimedAmount;

    if (claimAmount != 0) claimed[msg.sender][reward] = claimedAmount + claimAmount;

    if (time > refTime * 1e18) {
        uint256 rawEarned = earned(reward, msg.sender, balanceOf(msg.sender));
        uint256 savedAmount = saved[msg.sender][reward];
        uint256 maxClaimed = Math.min(rawEarned, claimableAmount);
        uint256 saveAmount = rawEarned > maxClaimed + savedAmount ? rawEarned - maxClaimed - savedAmount : 0;

        if (saveAmount != 0) {
            saved[msg.sender][reward] = savedAmount + saveAmount;
            reward.transfer(savings, saveAmount);
        }
    }
    
    if (claimAmount != 0) {
        reward.transfer(msg.sender, claimAmount);
        emit RewardPaid(reward, msg.sender, claimAmount);
    }
}
```

Due to this implementation, the `to` address, which receives the staked tokens (`stEXA`), cannot claim the rewards upon deposit. Consequently, `avgIndexes[to][reward]` is adjusted without the corresponding reward being claimed, which can lead to a potential loss of rewards for the depositing user.

## Impact
This flaw results in the loss of rewards for users upon depositing, as the rewards that would have been claimed for the `to` address are not processed correctly.

## Code Snippet
https://github.com/sherlock-audit/2024-07-exactly-stacking-contracts/blob/main/protocol/contracts/StakedEXA.sol#L129-L166
https://github.com/sherlock-audit/2024-07-exactly-stacking-contracts/blob/main/protocol/contracts/StakedEXA.sol#L371-L397

## Tool used

Manual Review

## Recommendation
To address this vulnerability, it's recommended to modify the `claim_()` function to accept an `account` parameter. This will allow rewards to be claimed not just for the caller but also for the specified address (`to`). Additionally, the following modification should be made to the `_update()` function:

```solidity
function _update(address from, address to, uint256 amount) internal override whenNotPaused {
    // SNIP...
    } else {
---    if (balance != 0) claim_(reward);
+++   if (balance != 0) claim_(reward, to); 
        uint256 numerator = avgIndexes[to][reward] * balance + rewards[reward].index * amount;
        avgIndexes[to][reward] = numerator == 0 ? 0 : (numerator - 1) / total + 1;
    }
    
    // SNIP...
}
```