Sleepy Denim Cricket

High

# Rewards `rate` calculation leads to under-allocation and funds stuck

## Summary
The calculation of the `rate` of a reward results in some funds not being distributed as expected and stuck in some cases.

## Vulnerability Detail
When `notifyRewardAmount` is called, the following calculation is done in order to determine the `rate` of the reward:
```solidity
if (block.timestamp >= rewardData.finishAt) {
	rewardData.rate = amount / rewardData.duration;
} else {
	uint256 remainingRewards = (rewardData.finishAt - block.timestamp) * rewardData.rate;
	rewardData.rate = (amount + remainingRewards) / rewardData.duration;     
}
```

If either `amount / rewardData.duration` or `(amount + remainingRewards) / rewardData.duration` are not whole divisions, there will be some tokens that won't be distributed as expected (up to duration amount) and that will remain stuck in the `StakedEXA` contract.

The previously mentioned situation breaks the following protocol invariant: 
```solidity
reward.balanceOf(address(stEXA)) - earnedRewardByAllStakers == 0
```

There are multiple situations in which this issue arises:

- During the normal `notifyRewardAmount` flow, if `1000e18` of reward A is intended to be distributed within `24 weeks`, `rewardData.rate * 24 weeks` should be equal to `1000e18`.  However, `rewardData.rate * 24 weeks` is equal to `999999999999989452800`.
- Suppose that the admin wants to finish the distribution of a reward. Again, a significant amount of tokens could be stuck in the contract and the only way to distribute them is by sending more tokens and enabling distribution again. Which is not expected.
- When there are tokens received from the market provided by the provider, if the amount sent is not divisible by the reward duration, the amount lost by the rounding will be stuck forever. This situation is complex because every time `harvest` is called, `notifyRewardAmount` is executed with the amount that results from the calculation:
```solidity
  function harvest() public whenNotPaused {
    Market memMarket = market;
    address memProvider = provider;
    uint256 assets = Math.min(
      memMarket.convertToAssets(memMarket.allowance(memProvider, address(this))),
      memMarket.maxWithdraw(memProvider)
    );
    uint256 amount = assets.mulWadDown(providerRatio);
    IERC20 providerAsset = IERC20(address(memMarket.asset()));
    uint256 duration = rewards[providerAsset].duration;
    if (duration == 0 || amount < rewards[providerAsset].duration) return;
    
    memMarket.withdraw(assets, address(this), memProvider);
    uint256 save = assets - amount;
    if (save != 0) memMarket.deposit(save, savings);
    
    notifyRewardAmount(providerAsset, amount, address(this));
  }
```

 Here we can see that the `amount` provided depends on the `market` which changes its rates in conversion permanently and it also depends on the `providerRatio`. Given that the protocol can't control when `harvest` is called, `notifyRewardAmount` is likely to be called with any `amount` as parameter.

The following PoC provides an accurate representation of this issue. The admin expects to finish the distribution of an asset by calling `finishDistribution` so the protocol updates the `finishAt` value and sends to the `saving` address the amount of tokens that hasn't been yet distributed.
```solidity
// To be added to "test/StakedEXA.t.sol"
/* Context:
 initialAmount = 1000e18
 duration = 24 weeks
*/
function testFinishDistributionKeepsRewardsStuck() external {
    uint256 assets = 1000e18;
    exa.mint(address(this), assets * 2);

    stEXA.deposit(assets , address(this));
    stEXA.deposit(assets, BOB);

    skip(minTime + 1);

    assertEq(rA.balanceOf(address(stEXA)), initialAmount);

    assertEq(stEXA.totalSupply(), assets * 2);

    stEXA.finishDistribution(rA);

    uint256 thisEarned = earned(rA, address(this));
    uint256 bobEarned = earned(rA, BOB);

    // this keeps failing but shouldn't
    assertEq(rA.balanceOf(address(stEXA)) - thisEarned - bobEarned, 0); 
  }

```

## Impact
Rewards distribution is not done as expected because a significant amount tokens is not distributed.
Not distributed rewards stuck in the contract.

## Code Snippet
https://github.com/sherlock-audit/2024-07-exactly-stacking-contracts/blob/3eb87e3edf3bcd57c4cc1c6a73e8255f575b76de/protocol/contracts/StakedEXA.sol#L344-L361
https://github.com/sherlock-audit/2024-07-exactly-stacking-contracts/blob/3eb87e3edf3bcd57c4cc1c6a73e8255f575b76de/protocol/contracts/StakedEXA.sol#L209-L229

## Tool used

Manual Review

## Recommendation
The mitigation could be different but here is mine:
Add a check in the `notifyRewardAmount` function to send the `amount % duration` amount to the `saving` address.

```diff
  function notifyRewardAmount(IERC20 reward, uint256 amount, address notifier) internal onlyReward(reward) {
    updateIndex(reward);
    RewardData storage rewardData = rewards[reward];
    if (block.timestamp >= rewardData.finishAt) {
      rewardData.rate = amount / rewardData.duration;
+     uint256 remainder = amount % rewardData.duration;
+     if(remainder != 0){
+         reward.transfer(savings, remainder);
+     }
    } else {
      uint256 remainingRewards = (rewardData.finishAt - block.timestamp) * rewardData.rate;
      rewardData.rate = (amount + remainingRewards) / rewardData.duration;
+     uint256 remainder = (amount + remainingRewards) % rewardData.duration;
+     if(remainder != 0){
+	      reward.transfer(savings, remainder);
+     }
    }
    
    if (rewardData.rate == 0) revert ZeroRate();
    if (
      rewardData.rate * rewardData.duration >
      reward.balanceOf(address(this)) - (address(reward) == asset() ? totalAssets() : 0)
    ) revert InsufficientBalance();
	
    rewardData.finishAt = uint40(block.timestamp) + rewardData.duration;
    rewardData.updatedAt = uint40(block.timestamp);
    
    emit RewardAmountNotified(reward, notifier, amount);
  }
```
